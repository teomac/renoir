WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

select = { "SELECT" }
from = { "FROM" }
where_keyword = { "WHERE" }
as_keyword = { "AS" }
join = { "JOIN" }
on = { "ON" }
asterisk = { "*" }
group_by_keyword = { "GROUP BY" }
having_keyword = { "HAVING" }

l_paren = { "(" }
r_paren = { ")" }

variable = @{ ('a'..'z' | "_") ~ ('a'..'z' | '0'..'9' | "_")* }
number = @{ 
    ('0'..'9')+ // Integer part
    ~ ("." ~ ('0'..'9')+)? // Optional decimal part
}
symbol = @{ ("^"| "+" | "-" | "*" | "/")}


// Aggregate function definition
agg_function = { ("MAX" | "MIN" | "AVG" | "SUM" | "COUNT") }
aggregate_expr = { agg_function ~ "(" ~ (asterisk | table_column | variable) ~ ")" }

operator = @{ ">=" | "<=" | "!=" | "<>" | ">" | "<" | "=" }

null_operator = @ { "IS NOT NULL" | "IS NULL" }

binary_op = { 
    ^"AND" | 
    ^"OR"
}

table_column = {
    variable ~ "." ~ variable
}

string_literal = @{ "'" ~ variable ~ "'" }

// Condition parts
condition = { ((table_column | variable | number | string_literal) ~ operator ~ (table_column | variable | number | string_literal) | ((table_column | variable) ~ null_operator)) }
where_conditions = { condition ~ (binary_op ~ condition)* }
where_expr = { where_keyword ~ where_conditions }

//Projections

column_item = {select_expr | aggregate_expr | table_column | variable }
column_with_alias = { column_item ~ (as_keyword ~ variable)? }
column_list = { column_with_alias ~ ("," ~ column_with_alias)* }
select_expr = { 
    (parenthesized_expr | column_operand) ~ 
    (symbol ~ (parenthesized_expr | column_operand))*
}

column_operand = { aggregate_expr | table_column | variable | number }
parenthesized_expr = { l_paren ~ select_expr ~ r_paren }


// Having clause

having_condition = { ((table_column | variable | number | string_literal | aggregate_expr) ~ operator ~ (table_column | variable | number | string_literal | aggregate_expr) | ((table_column | variable) ~ null_operator)) }
having_clause = { having_condition ~ (binary_op ~ having_condition)* }


// Group by 

group_by_list = { (table_column | variable) ~ ("," ~ (table_column | variable))* }
group_by_expr = { group_by_keyword ~ group_by_list ~ ( having_keyword ~ having_clause )? }


join_condition = { table_column ~ "=" ~ table_column ~ ("AND" ~ table_column ~ "=" ~ table_column)* }


from_expr = { from ~ scan_expr ~ join_expr* }
scan_expr = { variable ~ (as_keyword ~ variable)? }
join_expr = { join_kind? ~ join ~ scan_expr ~ on ~ join_condition }
join_kind = _{ ( InnerJoinKind | LeftJoinKind ) }
                InnerJoinKind = { ^"inner" }
                LeftJoinKind = { ^"left" ~ (^"outer")? }



query = {
    SOI ~
    select ~ (asterisk| column_list) ~
    from_expr ~
    where_expr? ~
    group_by_expr? ~
    EOI
}