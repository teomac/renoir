WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

// Basic building blocks
identifier = @{ ('a'..'z' | "_") ~ ('a'..'z' | '0'..'9' | "_")* }
number = @{ 
    ('0'..'9')+ // Integer part
    ~ ("." ~ ('0'..'9')+)? // Optional decimal part
}
boolean_keyword = @{ ^"true" | ^"false" }
symbol = @{ ("^"| "+" | "-" | "*" | "/")}
value = { boolean_keyword | number }

as_keyword = { "as" }
in_keyword = { "in" }
join = { "join" }
on = { "on" }
asterisk = { "*" }
group_keyword = { "group" }


query = {
    SOI ~
    from_clause  ~
    where_clause?  ~
    group_clause?  ~
    select_clause  ~
    EOI
}

// Basic clauses
from_clause = { 
    "from" ~ 
    scan_expr ~
    (join ~ scan_expr ~ on ~ join_condition)*
}

stream_input = @{ ('a'..'z' | "_") ~ ('a'..'z' | '0'..'9' | "_")* }

scan_expr = { 
    identifier ~ 
    (as_keyword ~ identifier)? ~ 
    in_keyword ~ 
    stream_input 
}

join_condition = {
    qualified_column ~ "==" ~ qualified_column
}

qualified_column = {
    identifier ~ "." ~ identifier
}

complex_op = {(qualified_column | identifier | number) ~ symbol ~ (qualified_column | identifier | number) }

// Aggregate expressions
aggregate_expr = { aggregate_func ~ "(" ~ (asterisk |qualified_column | identifier) ~ ")" }
aggregate_func = { "max" | "min" | "avg" | "count" | "sum" }

column_list = {
    column_item ~ ("," ~ column_item)*
}

column_item = {
    (aggregate_expr | complex_op | qualified_column | identifier) ~
    (as_keyword ~ identifier)?
}


select_clause = { "select" ~ (asterisk | column_list) }
where_clause = { "where" ~ where_conditions }

// Stream definitions
stream_identifier = @{ ('a'..'z' | "_") ~ ('a'..'z' | '0'..'9' | "_")* }
stream_type = { "Stream" }


// Binary logical operators
binary_op = { 
    ^"AND" | 
    ^"OR"
}

// Conditions for WHERE clause
condition = { (qualified_column | identifier | value) ~ comparison_op ~ (qualified_column | identifier | value) }
comparison_op = @{ ">=" | "<=" | "!=" | ">" | "<" | "==" | "="}
where_conditions = { condition ~ (binary_op ~ condition)* }

// Optional method chain syntax for filter and aggregation operations
method_chain = { "." ~ (filter_op | agg_op) }
filter_op = { "filter" ~ "(" ~ condition ~ ")" }
agg_op = { ("max" | "min" | "avg") ~ "(" ~ identifier ~ ")" }

having_condition = { (aggregate_expr | value | qualified_column | identifier) ~ comparison_op ~ (aggregate_expr | value | qualified_column | identifier ) }
having_clause = {"{"  ~ having_condition ~ (binary_op ~ having_condition)* ~ "}"}


group_columns = { (qualified_column | identifier)  ~ ("," ~ (qualified_column | identifier))* }
group_clause = { group_keyword  ~  group_columns ~ ( having_clause )? }