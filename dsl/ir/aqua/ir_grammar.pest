WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

// Basic building blocks
identifier = @{ ('a'..'z' | "_") ~ ('a'..'z' | '0'..'9' | "_")* }
number = @{ 
    ('0'..'9')+ // Integer part
    ~ ("." ~ ('0'..'9')+)? // Optional decimal part
}
boolean_keyword = @{ ^"true" | ^"false" }
symbol = @{ ("^"| "+" | "-" | "*" | "/")}
string = @{ "'" ~ ('a'..'z' | "_")* ~ "'" }
value = { boolean_keyword | number | string }

as_keyword = { "as" }
in_keyword = { "in" }
join = { "join" }
on = { "on" }
asterisk = { "*" }
group_keyword = { "group" }

left_parenthesis = { "(" }
right_parenthesis = { ")" }

null_op = @{ "is null" | "is not null" }

query = {
    SOI ~
    from_clause  ~
    where_clause?  ~
    group_clause?  ~
    select_clause  ~
    EOI
}

// Basic clauses
from_clause = { 
    "from" ~ 
    scan_expr ~
    (join ~ scan_expr ~ on ~ join_condition)*
}

stream_input = @{ ('a'..'z' | "_") ~ ('a'..'z' | '0'..'9' | "_")* }

scan_expr = { 
    identifier ~ 
    (as_keyword ~ identifier)? ~ 
    in_keyword ~ 
    stream_input 
}

join_condition = {
    qualified_column ~ "==" ~ qualified_column ~ ("&&" ~ qualified_column ~ "==" ~ qualified_column)*
}

qualified_column = {
    identifier ~ "." ~ identifier
}

// Arithmetic expressions for conditions
arithmetic_expr = {
    arithmetic_term ~ (symbol ~ arithmetic_term)*
}

arithmetic_term = {
    left_parenthesis ~ arithmetic_expr ~ right_parenthesis |
    arithmetic_operand
}

arithmetic_operand = {
    value |
    qualified_column |
    identifier |
    aggregate_expr
}

// Conditions for WHERE clause
condition = { 
    arithmetic_expr ~ comparison_op ~ arithmetic_expr | 
    (qualified_column | identifier) ~ null_op
}

comparison_op = @{ ">=" | "<=" | "!=" | ">" | "<" | "==" | "="}
where_conditions = { condition ~ (binary_op ~ condition)* }
where_clause = { "where" ~ where_conditions }

// Select clause expressions
select_expr = { complex_op | aggregate_expr | qualified_column | identifier }
column_operand = { aggregate_expr | qualified_column | identifier | number }
parenthesized_expr = { left_parenthesis ~ select_expr ~ right_parenthesis }

complex_op = { 
    (parenthesized_expr | column_operand) ~ 
    (symbol ~ (parenthesized_expr | column_operand))*
}

// Aggregate expressions
aggregate_expr = { aggregate_func ~ "(" ~ (asterisk | qualified_column | identifier) ~ ")" }
aggregate_func = { "max" | "min" | "avg" | "count" | "sum" }

column_list = {
    column_item ~ ("," ~ column_item)*
}

column_item = {
    (qualified_column | aggregate_expr | identifier |  complex_op) ~
    (as_keyword ~ identifier)?
}

select_clause = { "select" ~ (asterisk | column_list) }

// Binary logical operators
binary_op = { 
    ^"AND" | 
    ^"OR"
}

// Group by clause
having_condition = { (aggregate_expr | value | qualified_column | identifier) ~ ((comparison_op ~ (aggregate_expr | value | qualified_column | identifier)
                    ) | null_op) }
having_clause = {"{"  ~ having_condition ~ (binary_op ~ having_condition)* ~ "}"}

group_columns = { (qualified_column | identifier)  ~ ("," ~ (qualified_column | identifier))* }
group_clause = { group_keyword  ~  group_columns ~ ( having_clause )? }